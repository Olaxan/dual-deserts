#pragma kernel CSMakeVertices
#pragma kernel CSMakeTriangles

#include "/Includes/Volume.cginc"
#include "/Includes/Tables.cginc"

#define CENTER_BIAS

static const int xyNumThreads = 8;

struct Quad 
{
	int3 t0;
	int3 t1;
};

RWStructuredBuffer<int> indices;
RWStructuredBuffer<float3> vertices;
RWStructuredBuffer<float3> normals;
AppendStructuredBuffer<Quad> quads;

float isoScale;
float maxCornerDistance;
float centerBias;
float clampRange;

uint getVoxelIndex(uint3 id)
{
	return ((id.z * (isoSize - 1)) + id.y) * (isoSize - 1) + id.x;
}

bool SolveMatrix(in float3x3 A, in float3 b, out float3 vertex)
{
	float det = determinant(A);

	if (abs(det) <= 1e-12)
	{
		vertex = float3(0,0,0);
		return false;
	}

	vertex = float3
	(
		determinant(float3x3(b, A[1], A[2])),
		determinant(float3x3(A[0], b, A[2])),
		determinant(float3x3(A[0], A[1], b))
	) / det;

	return !(any(isnan(vertex)) || any(isinf(vertex)));
}

bool SolveError(in int N, in float3 A[numPoints], in float b[numPoints], out float3 vertex)
{
	if (N == 3)
	{
		float3x3 mat = float3x3(A[0], A[1], A[2]);
		float3 vec = float3(b[0], b[1], b[2]);
		return SolveMatrix(mat, vec, vertex);
	}

	float3x3 At_A;
	float3 At_b;

	[unroll(3)]
	for (int i = 0; i < 3; i++)
	{
		[unroll(3)]
		for (int j = 0; j < 3; j++)
		{
			float sum = 0;

			for (int k = 0; k < N; k++)
				sum += A[k][i] * A[k][j];

			At_A[i][j] = sum;
		}
	}

	[unroll(3)]
	for (int u = 0; u < 3; u++)
	{
		float sum = 0;

		for (int v = 0; v < N; v++)
			sum += A[v][u] * b[v];

		At_b[u] = sum;
	}

	return SolveMatrix(At_A, At_b, vertex);
}

[numthreads(xyNumThreads, xyNumThreads, 1)]
void CSMakeVertices(uint3 id : SV_DispatchThreadID)
{

	if (checkEdge(id, 1))
		return;

	uint index = getVoxelIndex(id);
	indices[index] = -1;

	bool inside[numCorners];
	int numInside = 0;

	for (int ci = 0; ci < numCorners; ci++)
	{
		inside[ci] = (isoDists[getVolumeIndex(id + corners[ci])] <= 0);
		if (inside[ci])
			numInside++;
	}

	if (numInside == 0 || numInside == numCorners)
		return;

	bool crossingCorners[numCorners] = { false, false, false, false, false, false, false, false };

	for (int e = 0; e < numEdges; e++)
	{
		if (inside[edges[e].x] != inside[edges[e].y])
		{
			crossingCorners[edges[e].x] = true;
			crossingCorners[edges[e].y] = true;
		}
	}

	float dists[numPoints];
	float3 norms[numPoints];

	float3 A[numPoints];
	float b[numPoints];

	int pointCount = 0;

	for (int cj = 0; cj < numCorners; cj++)
	{
		uint3 n_pos = id + corners[cj];
		float n_dist = isoDists[getVolumeIndex(n_pos)];
		float3 n_norm = isoNormals[getVolumeIndex(n_pos)];

		if (!crossingCorners[cj])
			continue;

		if (abs(n_dist) > maxCornerDistance)
			continue;

		float d = dot(n_norm, float3(n_pos)) - n_dist;
		float3 n = n_norm;
		dists[pointCount] = d;
		norms[pointCount] = n;
		pointCount++;
	}

	float3 voxelCenter = float3(id) + float3(0.5, 0.5, 0.5);

#ifdef CENTER_BIAS
	for (int ai = 0; ai < numAxes; ai++)
	{
		float3 n2 = centerBias * float3(axes[ai]);
		float d2 = dot(n2, voxelCenter);
		dists[pointCount] = d2;
		norms[pointCount] = n2;
		pointCount++;
	}
#endif

	float3 avgNormal = float3(0, 0, 0);
	for (int pi = 0; pi < pointCount; pi++)
	{
		float3 norm = norms[pi];
		avgNormal += norm;
		A[pi] = norm;
		b[pi] = dists[pi];
	}

	avgNormal /= float(pointCount);

	float3 vertex;
	if (SolveError(pointCount, A, b, vertex))
	{
		vertex = clamp(vertex, float3(id), float3(id + float3(clampRange, clampRange, clampRange)));
	}
	else 
		vertex = voxelCenter;

	vertex *= isoScale;

	int idx = vertices.IncrementCounter();
	vertices[idx] = vertex;
	normals[idx] = avgNormal;
	indices[index] = idx;
}

[numthreads(xyNumThreads, xyNumThreads, 1)]
void CSMakeTriangles(uint3 id : SV_DispatchThreadID)
{

	if (checkEdge(id, 2))
		return;

	int v0 = indices[getVoxelIndex(id)];
	
	if (v0 < 0)
		return;

	bool inside[numCorners] = { false, false, false, false, false, false, false, false };

	for (int ci = 0; ci < numCorners; ci++)
		inside[ci] = (isoDists[getVolumeIndex(id + corners[ci])] <= 0);

	for (int ai = 0; ai < numFarEdges; ai++)
	{
		uint2 edge = farEdges[ai];

		if (inside[edge.x] == inside[edge.y])
			continue;

		int v1, v2, v3;
		int s1, s2, s3;
		
		if (ai == 0)
		{
			v1 = indices[getVoxelIndex(id + neighbours[0])];
			v2 = indices[getVoxelIndex(id + neighbours[1])];
			v3 = indices[getVoxelIndex(id + neighbours[2])];
		}
		else if (ai == 1)
		{
			v1 = indices[getVoxelIndex(id + neighbours[0])];
			v2 = indices[getVoxelIndex(id + neighbours[3])];
			v3 = indices[getVoxelIndex(id + neighbours[4])];
		}
		else
		{
			v1 = indices[getVoxelIndex(id + neighbours[1])];
			v2 = indices[getVoxelIndex(id + neighbours[3])];
			v3 = indices[getVoxelIndex(id + neighbours[5])];
		}

		if (v1 < 0 || v2 < 0 || v3 < 0)
			continue;

		if (inside[edge.x] == (ai == 1))
		{
			Quad q1;
			q1.t0 = int3(v0,v1,v3);
			q1.t1 = int3(v0,v3,v2);
			quads.Append(q1);
		}
		else
		{
			Quad q2;
			q2.t0 = int3(v0,v3,v1);
			q2.t1 = int3(v0,v2,v3);
			quads.Append(q2);
		}
	}
}
